apply plugin: 'java'
//apply plugin: 'maven'
apply plugin: 'com.bmuschko.docker-remote-api'

import com.bmuschko.gradle.docker.tasks.container.*
import com.bmuschko.gradle.docker.tasks.image.*


group = 'noname'
version = '0.0.1-SNAPSHOT'

description = """"""

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

def portToSystemPropertyKey = [5432 : "postgres_port"]

task createMyAppContainer(type: DockerCreateContainer) {
    targetImageId { 'postgres:9.5' }

    portBindings = portToSystemPropertyKey.collect {port, hostSysProp -> "${System.getProperty(hostSysProp, '')}:${port}"}
}

task startMyAppContainer(type: DockerStartContainer) {
    dependsOn createMyAppContainer
    targetContainerId { createMyAppContainer.getContainerId() }
}

task stopMyAppContainer(type: DockerStopContainer) {
    targetContainerId { createMyAppContainer.getContainerId() }
}

task inspect(type: DockerInspectContainer) {
    dependsOn startMyAppContainer
    targetContainerId { createMyAppContainer.getContainerId() }
    onNext {
        println "PORTS expected: ${portToSystemPropertyKey}"
        println "PORTS actual: ${it.networkSettings.ports}"
        it.networkSettings.ports.getBindings().each { exposedPort, bindingArr ->

            def sysPropertyKey = portToSystemPropertyKey[exposedPort.getPort()]
            if ('' != sysPropertyKey) {
                println sysPropertyKey
                bindingArr.each { System.setProperty(sysPropertyKey, it.getHostPortSpec()) }
            }
        }

        println portToSystemPropertyKey.collect{ k, v -> "${k} -> ${System.getProperty(v)}" }
    }
}

task functionalTestMyApp(/*type: Test*/) {
    dependsOn startMyAppContainer
    dependsOn inspect
    finalizedBy stopMyAppContainer
}

test.dependsOn startMyAppContainer
test.dependsOn inspect
test.finalizedBy stopMyAppContainer
test.testLogging.showStandardStreams = true

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.bmuschko:gradle-docker-plugin:3.0.5'
    }
}

repositories {

    maven { url "http://repo.maven.apache.org/maven2" }
}

dependencies {
    compile group: 'org.apache.tomcat', name: 'tomcat-util', version:'7.0.55'
    compile group: 'org.apache.tomcat.embed', name: 'tomcat-embed-core', version:'7.0.55'
    compile group: 'org.postgresql', name: 'postgresql', version:'9.4.1212.jre7'
    compile group: 'org.flywaydb', name: 'flyway-core', version:'3.1'
    compile group: 'com.google.code.gson', name: 'gson', version:'2.8.0'
    compile group: 'org.springframework', name: 'spring-jdbc', version:'4.3.5.RELEASE'
    compile group: 'org.hibernate', name: 'hibernate-core', version:'5.2.6.Final'
    compile group: 'org.hibernate', name: 'hibernate-entitymanager', version:'5.2.6.Final'
    testCompile group: 'junit', name: 'junit', version:'4.11'
    testCompile group: 'com.jayway.restassured', name: 'rest-assured', version:'2.3.1'
}

// TODO: remove container on build failure?